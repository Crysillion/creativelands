id "ice_spike";

num randPrecision = 100;

//Free to tweak variables
num length = 20 + randomInt(randPrecision)/randPrecision*60;
num radius = 2 + randomInt(randPrecision)/randPrecision*2;
//Direction vector
num dx = (randomInt(randPrecision)/randPrecision-0.5)*1.5;
num dy = 0.5 + randomInt(randPrecision)/randPrecision;
num dz = (randomInt(randPrecision)/randPrecision-0.5)*1.5;


//Normalize vector
num mag = sqrt(pow(dx,2)+pow(dy,2)+pow(dz,2));
dx = dx/mag;
dy = dy/mag;
dz = dz/mag;

for(num l = 0; l <= length; l = l + 1) {

    //Reduce radius inversely to length
    num r = (radius/length * -l) + radius;

    //Generate sphere at each point
    for(num x = -r; x <= r; x = x + 1) {
        for(num y = -r; y <= r; y = y + 1) {
            for(num z = -r; z <= r; z = z + 1) {
                if (pow(x,2)+pow(y,2)+pow(z,2) < pow(r,2)) {
                    block(l*dx + x, l*dy + y, l*dz + z, "minecraft:packed_ice", false);
                }
            }
        }
    }
}